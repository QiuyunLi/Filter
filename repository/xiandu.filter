/*
 * Copyright (c) 2018, Jason Tsang.(https://github.com/gnastnosaj) All Rights Reserved.
 */

def baseUrl = 'https://gank.io/xiandu'

task('build') {
    connection {
        url baseUrl
        page {
            document {
                catalog {
                    def builder = { _href ->
                        return {
                            url _href

                            def current, next

                            def parser = { page ->
                                def items = select('div.xiandu_items div.xiandu_item')
                                items.each {
                                    def title = it.select('a.site-title').text()
                                    def href = it.select('a.site-title').attr('abs:href')
                                    def publish = it.select('small').text()

                                    page.data(
                                            'type': 'post',
                                            'id': title,
                                            'title': title,
                                            'href': href,
                                            'publish': publish
                                    )
                                }

                                next = select('a.button').attr('abs:href')
                            }

                            task('refresh') {
                                def result

                                current = _href

                                connection {
                                    url current
                                    result = page {
                                        document parser
                                    }
                                }

                                return result
                            }

                            task('loadMore') {
                                def result

                                current = next

                                connection {
                                    url current
                                    result = page {
                                        document parser
                                    }
                                }

                                return result
                            }

                            task('hasMore') {
                                return next && !next.allWhitespace
                            }

                            task('page') { href ->
                                return connection {
                                    url href

                                    task('entrance') {
                                        return _href.toString()
                                    }

                                    task('layout') {
                                        return 'webview'
                                    }
                                }
                            }
                        }
                    }

                    def list = select('div#xiandu_cat ul li a')
                    list.each {
                        connection(it.text(), builder(it.attr('abs:href')))
                    }
                }
            }
        }
    }
}